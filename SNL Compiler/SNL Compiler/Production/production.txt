1)     Program    ::=    ProgramHead  DeclarePart ProgramBody
2)     ProgramHead    ::=    PROGRAM    ProgramName
3)     ProgramName      ::=       ID
4)     DeclarePart    ::=    TypeDecpart  VarDecpart  ProcDecPart
5)     TypeDecpart        ::=    ? 
6)     TypeDecpart        ::= TypeDec        
7)     TypeDec  ::=     TYPE    TypeDecList        
8)     TypeDecList    ::=    TypeId = TypeDef ; TypeDecMore
9)     TypeDecMore    ::=     ? 
10)    TypeDecMore    ::= TypeDecList                
11)    TypeId        ::=     ID    
12)    TypeDef    ::=     BaseType
13)    TypeDef    ::=  StructureType 
14)    TypeDef    ::=  ID        
15)    BaseType    ::=    INTEGER
16)    BaseType    ::=  CHAR
17)    StructureType    ::=     ArrayType
18)    StructureType    ::=    RecType                        
19)    ArrayType    ::=    ARRAY [  Low  ..   Top  ] OF BaseType
20)    Low              ::=      INTC
21)    Top              ::=    INTC   
22)    RecType        ::=    RECORD  FieldDecList     END
23)    FieldDecList    ::=    BaseType  IdList  ;  FieldDecMore    
24)    FieldDecList    ::=    ArrayType IdList  ;  FieldDecMore
25)    FieldDecMore    ::=    ?                     
26)    FieldDecMore    ::= FieldDecList
27)    IdList        ::=     ID  IdMore                
28)    IdMore        ::=     ?                      
29)    IdMore        ::=   ,   IdList  
30)    VarDecpart        ::=     ?                   
31)    VarDecpart        ::= VarDec            
32)    VarDec    ::=    VAR  VarDecList            
33)    VarDecList    ::=    TypeDef    VarIdList  ;  VarDecMore
34)    VarDecMore    ::=      ?  
35밀  VarDecMore    ::=  VarDecList
36밀  VarIdList    ::=    ID  VarIdMore    
37밀  VarIdMore    ::=     ?                  
38밀  VarIdMore    ::=  ,  VarIdList                    
39)    ProcDecPart    ::=     ?                        
40)    ProcDecPart    ::=  ProcDec              
41)    ProcDec    ::=    PROCEDURE  ProcName (  ParamList  )   ;   ProcDecPart ProcBody ProcDecMore 
42)    ProcDecMore ::=    ?                      
43)    ProcDecMore ::=  ProcDec
44)    ProcName     ::=    ID    
45)    ParamList    ::=     ?                      
46)    ParamList    ::=   ParamDecList     
47)    ParamDecList    ::=    Param  ParamMore
48)    ParamMore    ::=     ?                      
49)    ParamMore    ::=  ;  ParamDecList           
50)    Param        ::=    TypeDef  FormList
51)    Param        ::= VAR TypeDef  FormList    
52)    FormList    ::=     ID  FidMore        
53)    FidMore    ::=     ?              
54)    FidMore    ::=  ,  FormList    
55)    ProcDecPart    ::=    DeclarePart   
56)    ProcBody        ::=    ProgramBody    
57)    ProgramBody    ::=    BEGIN  StmList END
58)    StmList        ::=    Stm        StmMore    
59)    StmMore        ::=     ?          
60)    StmMore        ::=   ;  StmList    
61)    Stm        ::=    ConditionalStm
62)    Stm        ::=  LoopStm            
63)    Stm        ::=  InputStm    
64)    Stm        ::=  OutputStm        
65)    Stm        ::=  ReturnStm        
66)    Stm        ::=  ID AssCall     
67)    AssCall    ::=      AssignmentRest        
68)    AssCall    ::=    CallStmRest            
69)    AssignmentRest    ::=  VariMore    :=  Exp    
70)    ConditionalStm    ::=    IF RelExp THEN  StmList ELSE StmList  FI
71)    LoopStm ::=    WHILE  RelExp DO StmList ENDWH                
72)    InputStm    ::=    READ  (   Invar  )     
73)    Invar         ::=   ID            
74)    OutputStm    ::=    WRITE  (  Exp  )
75)    ReturnStm    ::=    RETURN         
76)    CallStmRest    ::=    (   ActParamList   )    
77)    ActParamList   ::=     ?              
78)    ActParamList   ::=   Exp  ActParamMore
79)    ActParamMore   ::=   ?                  
80)    ActParamMore   ::=  ,   ActParamList    
81)    RelExp    ::=  Exp  OtherRelE
82)    OtherRelE    ::=  CmpOp   Exp         
83)    Exp        ::=  Term   OtherTerm
84)    OtherTerm    ::=  ?                      
85)    OtherTerm    ::=  AddOp   Exp
86)    Term        ::=   Factor   OtherFactor
87)    OtherFactor    ::=   ? 
88)    OtherFactor    ::=  MultOp  Term
89)    Factor        ::=   (  Exp  )
90)    Factor        ::=  INTC    
91)    Factor        ::=  Variable
92)    Variable    ::=   ID   VariMore
93)    VariMore    ::=     ? 
94)    VariMore    ::=   [   Exp   ]    
95)    VariMore    ::=  .  FieldVar
96)    FieldVar    ::=      ID   FieldVarMore    
97)    FieldVarMore   ::=     ?                  
98)    FieldVarMore   ::=  [  Exp   ]                
99)    CmpOp    ::=      <
100)   CmpOp    ::=  =    
101)   AddOp    ::=      +    
102)   AddOp    ::=      -
103)   MultOp    ::=      *
104밀 MultOp    ::=   /    